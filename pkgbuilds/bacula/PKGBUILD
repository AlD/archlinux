# Maintainer: Daniel Albers <daniel@lbe.rs>
# bacula-fd.service by Christian Hesse <arch@eworm.de>

pkgbase=bacula-common
_pkgbase=${pkgbase%-*}
pkgname=( "${_pkgbase}-common" "${_pkgbase}-console" "${_pkgbase}-fd" "${_pkgbase}-bat" )
pkgver=7.0.4
pkgrel=1
arch=(i686 x86_64)
pkgdesc='Bacula - A Network Backup Tool'
url="http://www.bacula.org"
license=('AGPL3')
depends=('qt4')
makedepends=('tcp_wrappers')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        '00-qmake4.patch'
        'bacula-fd.service')

md5sums=('219bde6b1f36625e69102c788bffa70b'
         '9ac88867a9ad3a4fe0486a26d0cdb542'
         '230754737aefdfb57973457acd3f0697')
sha1sums=('f0684a7b7288b1706128cd74d2f69b2c44bd06ec'
          '58a60e8af9b4735c564c7223c2bf0c25803927f3'
          'e1f038e7feca1042858ae9b4d27b84b36ae96aab')

_instdir="${startdir}/install"

build() {
    pushd "${srcdir}/${_pkgbase}-${pkgver}"

    patch -Np3 -i ${srcdir}/00-qmake4.patch || true

    ./configure                               \
    --enable-bat                              \
    --enable-client-only                      \
    --enable-smartalloc                       \
    --prefix=/usr                             \
    --sbindir=/usr/bin                        \
    --sysconfdir=/etc/${_pkgbase}             \
    --with-openssl                            \
    --with-scriptdir=/etc/${_pkgbase}/scripts \
    --with-systemd=/usr/lib/systemd/system/   \
    --with-tcp-wrappers                       \
    --with-x

    make DESTDIR="$_instdir" install
}

package_bacula-bat() {
  pkgdesc='Bacula - A Network Backup Tool (management GUI)'
  backup=('etc/bacula/bat.conf')
  depends=('bacula-common')

  install -D ${_instdir}/usr/bin/bat ${pkgdir}/usr/bin/bat
  install -D ${_instdir}/etc/bacula/bat.conf ${pkgdir}/etc/bacula/bat.conf
  install -D ${_instdir}/usr/share/man/man1/bat.1.gz ${pkgdir}/usr/share/man/man1/bat.1.gz

  install -D ${srcdir}/${_pkgbase}-${pkgver}/scripts/bat.desktop ${pkgdir}/usr/share/applications/bat.desktop
  install -D ${srcdir}/${_pkgbase}-${pkgver}/src/qt-console/images/bat_icon.png ${pkgdir}/usr/share/pixmaps/bat_icon.png
}

package_bacula-common() {
  pkgdesc='Bacula - A Network Backup Tool (common files)'

  install -D ${_instdir}/usr/lib/libbac-${pkgver}.so ${pkgdir}/usr/lib/libbac-${pkgver}.so
  install -D ${_instdir}/usr/lib/libbaccfg-${pkgver}.so ${pkgdir}/usr/lib/libbaccfg-${pkgver}.so
  install -D ${_instdir}/usr/lib/libbacfind-${pkgver}.so ${pkgdir}/usr/lib/libbacfind-${pkgver}.so
  #install -D ${_instdir}/usr/lib/libbacpy-${pkgver}.so ${pkgdir}/usr/lib/libbacpy-${pkgver}.so
  install -D ${_instdir}/etc/bacula/scripts/btraceback.gdb ${pkgdir}/etc/bacula/scripts/btraceback.gdb
  install -D ${_instdir}/usr/bin/btraceback ${pkgdir}/usr/btraceback
}

package_bacula-console() {
  pkgdesc='Bacula - A Network Backup Tool (management CLI)'
  backup=('etc/bacula/bconsole.conf')
  depends=('bacula-common')

  install -D ${_instdir}/usr/bin/bconsole ${pkgdir}/usr/bin/bconsole
  install -D ${_instdir}/etc/bacula/bconsole.conf ${pkgdir}/etc/bacula/bconsole.conf
}

package_bacula-fd() {
  pkgdesc='Bacula - A Network Backup Tool (file daemon)'
  backup=('etc/bacula/bacula-fd.conf')
  depends=('bacula-common' 'tcp_wrappers')

  install -D ${_instdir}/etc/bacula/bacula-fd.conf ${pkgdir}/etc/bacula/bacula-fd.conf
  install -D ${_instdir}/usr/bin/bacula-fd ${pkgdir}/usr/bin/bacula-fd
  install -D ${_instdir}/usr/share/man/man8/bacula-fd.8.gz ${pkgdir}/usr/share/man/man8/bacula-fd.8.gz
  install -D ${_instdir}/usr/lib/bpipe-fd.so ${pkgdir}/usr/lib/bpipe-fd.so
  install -D ${srcdir}/bacula-fd.service ${pkgdir}/usr/lib/systemd/system/bacula-fd.service
}
