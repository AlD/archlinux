# Contributor: Dogukan Korkmazturk <d.korkmazturk@gmail.com>
# Contributor: Stephane Travostino <stephane.travostino@gmail.com>
# Contributor: Calogero Lo Leggio <kalos@autistici.org>
# Contributor: Daniel Albers <daniel@lbe.rs>

pkgname=bacula-bat
pkgver=5.2.13
pkgrel=2
pkgdesc="Bacula Administration Tool Console"
arch=(i686 x86_64)
url="http://www.bacula.org"
license=('AGPL3')
depends=('qt4')
backup=(etc/bat.conf)
source=("http://downloads.sourceforge.net/sourceforge/bacula/bacula-${pkgver}.tar.gz"
        "00-qmake4.patch")
md5sums=('43417bae0c221afb1f30a581c9e0f2fe'
         '9ac88867a9ad3a4fe0486a26d0cdb542')
sha1sums=('30b1eb2efb515138807163d046f675eaa29fad1c'
          '58a60e8af9b4735c564c7223c2bf0c25803927f3')

build() {
    pushd "${srcdir}/bacula-${pkgver}"

    patch -Np3 -i ../00-qmake4.patch

    ./configure --prefix=/usr --sbindir=/usr/bin \
    --enable-client-only --enable-bat \
    --with-x --with-openssl \
    --sysconfdir=/etc/

    make
}

package() {
    # install only bat files
    mkdir -p ${pkgdir}/usr/bin/ ${pkgdir}/usr/lib/ ${pkgdir}/etc/
    mkdir -p ${pkgdir}/usr/share/man/man1/ ${pkgdir}/usr/share/applications/ ${pkgdir}/usr/share/pixmaps/
    install -m755 ${srcdir}/bacula-${pkgver}/src/qt-console/.libs/bat ${pkgdir}/usr/bin/bat
    install -m755 ${srcdir}/bacula-${pkgver}/src/lib/.libs/*-*.so ${pkgdir}/usr/lib
    install -m755 ${srcdir}/bacula-${pkgver}/src/findlib/.libs/*-*.so ${pkgdir}/usr/lib
    install -m644 ${srcdir}/bacula-${pkgver}/src/qt-console/bat.conf ${pkgdir}/etc/bat.conf
    install -m644 ${srcdir}/bacula-${pkgver}/manpages/bat.1 ${pkgdir}/usr/share/man/man1/bat.1
    install -m644 ${srcdir}/bacula-${pkgver}/scripts/bat.desktop ${pkgdir}/usr/share/applications/bat.desktop
    install -m644 ${srcdir}/bacula-${pkgver}/src/qt-console/images/bat_icon.png ${pkgdir}/usr/share/pixmaps/bat_icon.png
}
